import groovy.json.JsonSlurper

plugins {
   id 'java'
   id 'com.github.johnrengelman.shadow' version '7.1.2'
}

def pkgJson = new JsonSlurper().parseText(file('../package.json').text)
def props = [yamjsVersion: pkgJson.version]

dependencies {
   // common lib deps
   implementation project(':common')
   implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"

   // graal deps
   implementation "org.graalvm.js:js:${graalVersion}"
   implementation "org.graalvm.sdk:graal-sdk:${graalVersion}"
   implementation "org.graalvm.truffle:truffle-api:${graalVersion}"

   // platform-specific deps
   compileOnly 'com.github.Minestom:Minestom:89a09f326e'
}

jar {
   archiveBaseName.set('yamjs')
   archiveClassifier.set('minestom')
   archiveVersion.set(pkgJson.version)
}

shadowJar {
   mergeServiceFiles()
}

processResources {
   // populate version
   filesMatching('**/extension.json') {
      expand props
   }
}

repositories {
   mavenCentral()
   maven { url = 'https://jitpack.io' }
   maven { url = 'https://repo.spongepowered.org/maven' }
}
